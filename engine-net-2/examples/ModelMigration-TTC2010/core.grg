using original_minimal_metamodel__ecore, dump_helper;

rule dump_addTransitionsToPartitions {
	state1:_StateVertex <-:_Transition_source- transition:_Transition;
	state2:_StateVertex <-:_Transition_target- transition;

	state1 <-:_Partition_contents- partition:_Partition -:_Partition_contents-> state2;

	negative {
		partition -:_Partition_contents-> transition;
	}

	modify {
		partition -:_Partition_contents-> transition;
	}
}

rule dump_guardedTransitionEdges {
	state1:_StateVertex;
	state2:_StateVertex;

	state1 -:_StateVertex_incoming-> transition:_Transition <-:_StateVertex_outgoing- state2;
	
	transition -:_Transition_guard-> guard:_Guard;
	guard -:_Guard_expression-> expr:_BooleanExpression;

	modify {
		delete(transition, guard, expr);

		state1 -e:dump_transition-> state2;

		eval {
			e.name = guard._name;
		}
	}
}

rule dump_transitionEdges {
	state1:_StateVertex;
	state2:_StateVertex;

	state1 -:_StateVertex_incoming-> transition:_Transition <-:_StateVertex_outgoing- state2;

	modify {
		delete(transition);

		state1 -:dump_transition-> state2;
	}
}

